\newpage

\section{Appendix}

\subsection{Compression.hs}
\label{A:comp}

\begin{minted}[bgcolor=bg,
    linenos, breaklines]{haskell}
{-# OPTIONS_GHC -fplugin Plugin.InversionPlugin #-}
{-# OPTIONS_GHC -Wno-incomplete-patterns #-}
{-# OPTIONS_GHC -Wno-overlapping-patterns #-}
module Compression where

import Prelude hiding ((++), map, takeWhile, dropWhile, length, head)

-----------------------------------------------------------
-- Version without prelude functions:
-----------------------------------------------------------
(++) :: [a] -> [a] -> [a]
[] ++ ys = ys
(x:xs) ++ ys = x : (xs ++ ys)

map :: (a -> b) -> [a] -> [b]
map _ []     = []
map f (x:xs) = f x : map f xs

head :: [a] -> a
head (x:_) = x
head [] = error "bad head"

length :: [a] -> Int
length [] = 0
length (x:xs) = 1 + (length xs)

takeWhile               :: (a -> Bool) -> [a] -> [a]
takeWhile _ []          =  []
takeWhile p (x:xs) = case p x of
  True -> x : takeWhile p xs
  False -> []

dropWhile               :: (a -> Bool) -> [a] -> [a]
dropWhile _ []          =  []
dropWhile p xs@(x:xs') = case p x of
  True -> dropWhile p xs'
  False -> xs

splitList :: [Int] -> [[Int]]
splitList [] = []
splitList [x] = [[x]]
splitList l@(x:_) = (takeWhile eqX l) : (splitList (dropWhile eqX l))
  where
    eqX = (\y -> x == y)

mergeLists :: [Int] -> [Int] -> [Int]
mergeLists [] [] = []
mergeLists (x:xs) (y:ys) = [x,y] ++ (mergeLists xs ys)
mergeLists [] y = y
mergeLists x [] = x

runLengthEncoder :: [Int] -> [Int]
runLengthEncoder xs = mergeLists digits times
  where
    subLists = splitList xs
    digits = map head subLists
    times = map length subLists
\end{minted}



%%==============================================================================
%%==============================================================================

\subsection{Encryption.hs}
\label{A:encryp}

\begin{minted}[bgcolor=bg,
    linenos, breaklines]{haskell}
module Encryption where

import Prelude hiding ((++), map, takeWhile, dropWhile, length, head)

import Data.Bits
import Data.Word

------------------------------------------------------------------------------
-- Implementing the tiny encryption algorithms by David Wheeler, Roger Needham
------------------------------------------------------------------------------

-- 128 bit key
data TEAKey = TEAKey {-# UNPACK #-} !Word32 {-# UNPACK #-} !Word32 {-# UNPACK #-} !Word32 {-# UNPACK #-} !Word32
  deriving (Show)


secretKey :: TEAKey
secretKey = (TEAKey  0xdeadbeef 0xdeadbeef 0xdeadbeef 0xdeadbeef)

delta :: Word32
delta = 0x9e3779b9

myData :: Word64
myData = 0xbadf00000000000d

rounds :: Int
rounds = 32

-- Plugin cannot handle Data.Bits, Data.Word :(
teaEncrypt :: TEAKey -> Word64 -> Word64
teaEncrypt (TEAKey k0 k1 k2 k3) v = doCycle rounds 0 v0 v1 where
  v0, v1 :: Word32
  v0 = fromIntegral v
  v1 = fromIntegral $ v `shiftR` 32
  doCycle :: Int -> Word32 -> Word32 -> Word32 -> Word64
  doCycle 0 _ v0 v1 = (fromIntegral v1 `shiftL` 32)
                      .|. (fromIntegral v0 .&. 0xffffffff)
  doCycle n sum v0 v1 = doCycle (n - 1) sum' v0' v1'
    where
      sum' = sum + delta
      v0' = v0 + (((v1 `shiftL` 4) + k0) `xor` (v1 + sum') `xor` ((v1 `shiftR` 5) + k1))
      v1' = v1 + (((v0 `shiftL` 4) + k2) `xor` (v0 + sum') `xor` ((v0 `shiftR` 5) + k3))
\end{minted}




%%==============================================================================
%%==============================================================================


\subsection{Simulation.hs}
\label{A:sim}

\begin{minted}[bgcolor=bg,
    linenos, breaklines]{haskell}
{-# OPTIONS_GHC -fplugin Plugin.InversionPlugin #-}
{-# OPTIONS_GHC -Wno-incomplete-patterns #-}
{-# OPTIONS_GHC -Wno-overlapping-patterns #-}
module Simulation where

import Prelude hiding ((++), lookup, Maybe(..), length)

-----------------------------------------------------------
-- Implementing a simple simulation of free falling objects
-----------------------------------------------------------

data Height = Height Int
  deriving (Show)
data Velocity = Velocity Int
  deriving (Show)
data Time = Time Int
  deriving (Show)
data TimeEnd = TimeEnd Int
  deriving (Show)

freeFall :: (Height, Velocity, Time, TimeEnd) -> (Height, Velocity, Time, TimeEnd)
freeFall current@((Height h), (Velocity v), (Time t), (TimeEnd tEnd)) =
  case t == tEnd of
    True -> current
    False -> freeFall ((Height h'), (Velocity v'), (Time t'), (TimeEnd tEnd))
      where
        v' = v + 10
        h' = h - v' + 5
        t' = t + 1
\end{minted}




%%==============================================================================
%%==============================================================================

\subsection{PartialInverses.hs}
\label{A:part_inv}

\begin{minted}[bgcolor=bg,
    linenos, breaklines]{haskell}
{-# OPTIONS_GHC -fplugin Plugin.InversionPlugin #-}
{-# OPTIONS_GHC -Wno-incomplete-patterns #-}
{-# OPTIONS_GHC -Wno-overlapping-patterns #-}
module PartialInverses where

import Prelude hiding (length)

--------------------------------------------------------------------------
-- Implement insertAt from which we will generate a partial inverse
-- removeAt. By fixing the index argument and giving a list, removeAt will
-- return the element at that index of the list and the remaining list
--------------------------------------------------------------------------

length :: [a] -> Int
length [] = 0
length (_:xs) = 1 + (length xs)

insertAt :: Int -> Int -> [Int] -> [Int]
insertAt 0 x xs = (x:xs)
insertAt i x l@(y:ys) =
  case i < 0 of
    True -> error "invalid index"
    False ->
      case i > (length l) of
        True -> error "invalid index"
        False -> y : (insertAt (i-1) x ys)
\end{minted}




%%==============================================================================
%%==============================================================================

\vspace{0.3cm}
\vspace{0.3cm}

\subsection{Main.hs}
\label{A:main}

\begin{minted}[bgcolor=bg,
    linenos, breaklines]{haskell}
{-# LANGUAGE TemplateHaskell, FlexibleContexts, ViewPatterns #-}
{-# OPTIONS_GHC -Wno-orphans #-}
module Main where

import Plugin.InversionPlugin

import Simulation
import Compression
import PartialInverses
import Criterion.Main
import Criterion.Types

import Prelude hiding (map, lookup, (++), last)

main :: IO ()
main = do
  putStrLn "Benchmarking automatically generated Inverses"
  defaultMainWith
    (defaultConfig {reportFile = Just "benchmarks.html",
                    csvFile = Just "benchmark-inverses.csv"}) $
    [bgroup "removeAt1" [ bench "4 [1,2,3,4,5,6]"
                          $ nf (\x -> take 1 (removeAt1 4 x)) [1,2,3,4,5,6],
                          bench "0 [3,2,1]"
                          $ nf (\x -> take 1 (removeAt1 0 x)) [3,2,1],
                          bench "50 [0..300]"
                          $ nf (\x -> take 1 (removeAt1 50 x)) [0..300]
                        ],
     bgroup "removeAt1Manual" [ bench "4 [1,2,3,4,5,6]"
                                $ nf (\x -> removeAt1Manual 4 x) [1,2,3,4,5,6],
                                bench "0 [3,2,1]"
                                $ nf (\x -> removeAt1Manual 0 x) [3,2,1],
                                bench "50 [0..300]"
                                $ nf (\x -> removeAt1Manual 50 x) [0..300]
                        ],
     bgroup "runLengthDecoder" [ bench "[1,5]" $
                                 nf (\x -> take 1 (runLengthDecoder x)) [1,5],
                                 bench "[1,5,2,2,3,3]" $
                                 nf (\x -> take 1 (runLengthDecoder x)) [1,5,2,2,3,3]
                               ],
     bgroup "runLengthDecoderManual" [ bench "[1,5]" $
                                       nf (\x -> runLengthDecoderManual x) [1,5],
                                       bench "[1,5,2,2,3,3]" $
                                       nf (\x -> runLengthDecoderManual x)
                                       [1,5,2,2,3,3]
                                     ]
    ]


split :: [Int] -> [([Int], [Int])]
split = $(inv '(++))

-----------------------------------------------------------
-- Simulations
-----------------------------------------------------------

freeFallInv :: (Height, Velocity, Time, TimeEnd)
             -> [(Height, Velocity, Time, TimeEnd)]
freeFallInv = $(inv 'freeFall)

fallStart :: (Height, Velocity, Time, TimeEnd)
fallStart = ((Height 176), (Velocity 0), (Time 0), (TimeEnd 3))

fallDown :: (Height, Velocity, Time, TimeEnd)
fallDown = freeFall fallStart

fallUp :: [(Height, Velocity, Time, TimeEnd)]
fallUp = take 5 $ freeFallInv fallDown

-----------------------------------------------------------
-- Compressions
-----------------------------------------------------------

runLengthDecoder :: [Int] -> [[Int]]
runLengthDecoder = $(inv 'runLengthEncoder)

-- implemented for benchmarking manual vs automatic generated inverse
runLengthDecoderManual :: [Int] -> [Int]
runLengthDecoderManual [] = []
runLengthDecoderManual [x] = error "Invalid encoding"
runLengthDecoderManual (x:(y:ys)) = (take y $ repeat x) ++ runLengthDecoderManual ys

dataToCompress :: [Int]
dataToCompress = [1,1,1,1,1,1,1,2,2,3,3,3,5]

encoded :: [Int]
encoded =  runLengthEncoder dataToCompress

decoded :: [[Int]]
decoded = take 1 $ runLengthDecoder encoded

-----------------------------------------------------------
-- Partial Inverses
-----------------------------------------------------------

-- partial inverse of insertAt fixing the first argument (index)
removeAt1 :: Int -> [Int] -> [(Int, [Int])]
removeAt1 = $(partialInv 'insertAt [1])

-- implemented for benchmarking manual vs automatic generated inverse
removeAt1Manual :: Int -> [Int] -> (Int, [Int])
removeAt1Manual _ [] = error "empty list"
removeAt1Manual 0 (x:xs) = (x, xs)
removeAt1Manual i (x:xs) = removeAt1Manual (i-1) xs

removeAt1Example :: [(Int, [Int])]
removeAt1Example = (take 1 (removeAt1 3 [1,2,3,4,5]))
\end{minted}

\newpage

\subsection{Error Messages}
\label{A:error}

\begin{figure}[h]
\begin{minted}[bgcolor=bg,breaklines]{text}
/home/pt/repos/inversion-plugin/firststeps/src/Compression.hs:1:1: error:
    No inverse available for: $
  |
1 | {-# OPTIONS_GHC -fplugin Plugin.InversionPlugin #-}
  | ^
Failed, no modules loaded.
\end{minted}
\captionof*{table}{Error Message encountered while programming the run length encoder}
\end{figure}

\begin{figure}[h]
\begin{minted}[bgcolor=bg,breaklines]{text}
 GHCi, version 8.10.4: https://www.haskell.org/ghc/  :? for help
[1 of 2] Compiling Compression      ( /home/pt/repos/inversion-plugin/firststeps/src/Compression.hs, interpreted )
ghc: panic! (the 'impossible' happened)
  (GHC version 8.10.4:
	splitFunTy
  ListFL (ListFL a_ab0v)
  --> ((ListFL a_ab0v --> ListFL Any) --> ListFL Any)
  Call stack:
      CallStack (from HasCallStack):
        callStackDoc, called at compiler/utils/Outputable.hs:1179:37 in ghc:Outputable
        pprPanic, called at compiler/types/Type.hs:1018:32 in ghc:Type

Please report this as a GHC bug:  https://www.haskell.org/ghc/reportabug
\end{minted}
\captionof*{table}{Error Message encountered while programming the run length encoder}
\end{figure}
